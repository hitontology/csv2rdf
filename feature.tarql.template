PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX hito: <http://hitontology.eu/ontology/>                                                                      
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX bb: <http://www.snik.eu/ontology/bb/>

CONSTRUCT
{
    ?f a hito:FeatureClassified;
	hito:fClaFrom ?cat;
	rdfs:comment ?co;
	skos:definition ?def;
	hito:internalId ?num;
	dcterms:source ?dcts;
	rdfs:label ?len;
	rdfs:label ?lde.
}
FROM <file:../feature.csv>
WHERE
{
   BIND (URI(CONCAT(tarql:expandPrefix('hito'),?uri)) AS ?f)
   BIND (IF(?comment != '', STRLANG(?comment,"en"), ?nothing) AS ?co)
   BIND (IF(?definition!= '', STRLANG(?definition,"en"), ?nothing) AS ?def)
   BIND (IF(?en != '', STRLANG(?en,"en"), ?nothing) AS ?len)
   BIND (IF(?de != '', STRLANG(?de,"de"), ?nothing) AS ?lde)
   BIND (URI(CONCAT(tarql:expandPrefix('hito'),"{SUB}FeatureCatalogue")) AS ?cat)
   BIND (IF(BOUND(?n)&&(?n != ''), ?n, ?nothing) AS ?num)
   BIND (IF(BOUND(?dctsource)&&(?dctsource != ''), URI(tarql:expandPrefixedName(?dctsource)), ?nothing) AS ?dcts)
}

